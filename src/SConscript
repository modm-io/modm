#!/usr/bin/env python
# 
# Copyright (c) 2009, Roboterclub Aachen e.V.
# All rights reserved.
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
# 
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of the Roboterclub Aachen e.V. nor the
#       names of its contributors may be used to endorse or promote products
#       derived from this software without specific prior written permission.
# 
# THIS SOFTWARE IS PROVIDED BY ROBOTERCLUB AACHEN E.V. ''AS IS'' AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL ROBOTERCLUB AACHEN E.V. BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

import os

Import('env')

# the buildpath of the library should only be used when building
# the library. Therefore a copy is made, so that the original environment
# remains untouched
envOld = env
env = env.Clone()
env.Append(CPPPATH = env['XPCC_BUILDPATH'])

# find all files, except the one located in the 'test' folders
scan = env.FindFiles(path = '.', unittest=False)

sources = scan.sources
defines = scan.defines

# Platform Files
architecture_path = os.path.join('.', 'xpcc', 'architecture')
p_sources, p_defines = env.GeneratePlatform(architecture_path)

# add sources to xpcc library in order for them to be built
sources.append(p_sources)

# hand defines to gcc through the -D flag for both xpcc and user code
envOld.Append(CPPDEFINES = p_defines)
env.Append(CPPDEFINES = p_defines)

# Update Enivronment Vars set by env.GeneratePlatform for "envOld"
# TODO: check if GeneratePlatform could just be called through the
#       oldEnv
if 'LINKPATH' in env:
	envOld['LINKPATH'] = env['LINKPATH']
if 'LINKFILE' in env:
	envOld['LINKFILE'] = env['LINKFILE']
if 'DEVICE_SIZE' in env:
	envOld['DEVICE_SIZE'] = env['DEVICE_SIZE']

# External Library Files
sources, defines, includes = env.SConscript(
		os.path.join(env['XPCC_ROOTPATH'], 'ext', 'SConscript'),
		exports = 'env sources defines')

for path in includes:
	envOld.AppendUnique(CPPPATH = os.path.join(env['XPCC_ROOTPATH'], 'ext', path))

# Generate Library
library = env.Library(target = 'xpcc', source = sources)

result = (library, defines)
Return('result')
