// coding: utf-8
// ----------------------------------------------------------------------------
/* Copyright (c) 2013, Roboterclub Aachen e.V.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *	 * Redistributions of source code must retain the above copyright
 *	   notice, this list of conditions and the following disclaimer.
 *	 * Redistributions in binary form must reproduce the above copyright
 *	   notice, this list of conditions and the following disclaimer in the
 *	   documentation and/or other materials provided with the distribution.
 *	 * Neither the name of the Roboterclub Aachen e.V. nor the
 *	   names of its contributors may be used to endorse or promote products
 *	   derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY ROBOTERCLUB AACHEN E.V. ''AS IS'' AND ANY
 * EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL ROBOTERCLUB AACHEN E.V. BE LIABLE FOR ANY
 * DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
// ----------------------------------------------------------------------------

	.section .fastcode, "x"
	.syntax	unified
	.align	4
	.thumb
	
	// ------------------------------------------------------------------------
	/**
	 * @brief	Simple delay loop (in microseconds resolution)
	 * 
	 * Use Timer-Interrupts etc. for delays whenever possible. This delay-loop
	 * is very inaccurate when interrupts or DMA transfers occur at the same
	 * time. This is because only the number of cycles inside this loop are
	 * counted. External interruptions go unnoticed and will prolong the delay
	 * time.
	 * 
	 * C equivalent:
	 * @code
	 * extern "C" void _delay_us(uint32_t us);
	 * @endcode
	 * 
	 * The function is put into the .fastcode section which resides in RAM to
	 * be sure that it is executed without any waitstates etc. Otherwise
	 * the delay variables had to be adapted every ARM type.
	 * 
	 * An example are the AT91SAM types, which aren't able to run ARM code from
	 * Flash at above 20-30 Mhz without wait-states. Of course this changes
	 * the executing timing which we would like to avoid here to get
	 * at least an reasonably accurate delay. 
	 * 
	 * @see		_delay_ms
	 * @author	Fabian Greif
	 */
	.global	_delay_us
	.type	_delay_us, %function
	.thumb_func
	.func	_delay_us
_delay_us:
	// Example:
	// F_CPU / 1000000 = cycles per us => 72,000,000 / 1,000,000 = 72
%# Different _delay_us implementations depending on core architecture
%% if target is cortex_m0
	// 72 / 4 cycles per loop = 18
	movs	r1, #(F_CPU / 1000000 / 4)
	muls	r1, r0, r1
	beq		2f
1:
	subs	r1, r1, #1
	bne		1b
2:
	bx		lr
%% elif target is cortex_m3 or target is cortex_m4
	%% if target is stm32f3
	// function resides in Core-Coupled memory, which is twice as fast as SRAM
	mov		r1, #(F_CPU / 1000000 / 5)
	%% else
	// 72 / 6 cycles per loop = 12
	mov		r1, #(F_CPU / 1000000 / 12)
	%% endif
	muls	r1, r0, r1
1:
	itt		ne
	subsne	r1, r1, #1
	nop
	bne		1b
	bx		lr
%% endif
	.size	_delay_us, . - _delay_us
	.endfunc
	
	// ------------------------------------------------------------------------
	/**
	 * @brief	Simple delay loop (in milliseconds resolution)
	 * 
	 * C equivalent:
	 * @code
	 * extern "C" void _delay_ms(uint32_t ms);
	 * @endcode
	 * 
	 * @see		_delay_us
	 * @author	Fabian Greif
	 */
	.global	_delay_ms
	.type	_delay_ms, %function
	.thumb_func
	.func	_delay_ms
_delay_ms:
	// see _delay_us
%# Different _delay_us implementations depending on core architecture
%% if target is cortex_m0
	ldr		r1, =(F_CPU / 1000 / 4)
	muls	r1, r0, r1
	beq		2f
1:
	subs	r1, r1, #1
	bne		1b
2:	bx		lr
%% elif target is cortex_m3 or target is cortex_m4
	%% if target is stm32f3
	// function resides in Core-Coupled memory, which is twice as fast as SRAM
	ldr		r1, =(F_CPU / 1000 / 4)
	%% else
	ldr		r1, =(F_CPU / 1000 / 8)
	%% endif
	muls	r1, r0, r1
1:
	itt		ne
	subsne	r1, r1, #1
	bne		1b
	bx		lr
%% endif
	.size	_delay_ms, . - _delay_ms
	.endfunc

	.end
